---
slug: /comlink
---

# Comlink

## Overview

Comlink is description language for autonomous APIs. It allows you to declaratively describe your API integrations in a way that separates the **design-time semantics** from the **run-time implementation details**. In other words, Comlink allows for defining the words we use to describe the capability of an API separate from the details for implementing that capability.

When API description formats don't make this seperations and combine design-time and run-time details, it creates challenges for developers.

* **Every API needs a its own OpenAPI file to describe the implementation**. This is one of the biggest struggles developers have when they're integrating with APIs. Since the semantics and implementation are mixed together, developers have to write separate integrations for each API they use even when those APIs provide the same capabilities. This takes time and money to build, maintain, and evolve these integrationsâ€”time and money that could be spent elsewhere.
* **Nothing can change at runtime**. Since semantic and implementation details are mixed together, nothing can change at runtime. This makes our code rigid and make it challenging to react to what's happening when our software is running.

Comlink addresses these problems by separating the design-time and run-time descriptions by providing Comlink Profile for describing business capabilities and Comlink Map for implementation details and how to map API providers and their services to business capabilities.

## Comlink Language References

To learn how to use the Comlink language with Superface and our tools and services, check out our [guides](../guides/how-to-create.md).

* [Comlink Profile](./references/profile.mdx) - describe business capabilities
* [Comlink Map](./references/map.mdx) - describe implementation details
* [Comlink Provider](./references/provider.mdx) - define provider definitions

If you're a tooling author, please refer to our [formal language specifications](./specifications.mdx).

## Quick Examples

These are Comlink examples at a glance. They show how you can use Comlink to separate the capability from the implementation by using the Complink Profile to describe the capability and using the Comlink Map to describe the implementation.

### Profile for sending emails

Here is an example of a Comlink Profile for sending emails. The use-case, inputs, result, and error are defined apart from implementation details. This profile declaratively describes the use-case so it can be reused across different API providers.

```hcl title="Profile for sending email"
"""
Send Email
Send one transactional email
"""

name = "communication/send-email"
version = "1.1.1"

"""
Send transactional email to one recipient

Email can contain text and/or html representation
"""
usecase SendEmail unsafe {
  input {
    from!
    to!
    subject!
    text
    html
  }

  result {
    messageId
  }

  error {
    title
    detail
  }
}
```

### Mapping an email provider to the send email profile

This is a Comlink Map that maps the inputs, result, and error defined in a Comlink Profile to the actual implementation which would include HTTP requests and responses and their respective JSON bodies.

```hcl title="Map Postmark provider to Send Email profile"
// API Reference: https://postmarkapp.com/developer/api/overview

profile = "communication/send-email@1.1"
provider = "postmark"

map SendEmail {
  http POST "/email" {
    security "server_token"
    
    request "application/json" {
      body {
        From = input.from
        To = input.to
        Subject= input.subject
        TextBody = input.text
        HtmlBody = input.html
      }
    }
    
    response 200 "application/json" {
      map result {
        messageId = body.MessageID
      }
    }

    response 422 "application/json" {
      map error {
        title = "Invalid inputs"
        detail = body.Message
      }
    }

    response 401 "application/json" {
      map error {
        title = "Unauthorized"
        detail = body.Message
      }
    }

    response 403 "application/json" {
      map error {
        title = "Forbidden"
        detail = body.Message
      }
    }

    response 500 "application/json" {
      map error {
        title = "Internal server Error"
        detail = body.Message
      }
    }
  }
}
```

### Provider definition for an email provider

Comlink keeps the provider details separate as well so they can be reused in other Comlink Maps. This example shows the Provider Definition for Postmark which includes their services and security schemes.

```hcl title="Provider definition for Postmark"
{
  "name": "postmark",
  "services": [
    {
      "id": "default",
      "baseUrl": "https://api.postmarkapp.com"
    }
  ],
  "defaultService": "default",
  "securitySchemes": [
    {
      "id": "server_token",
      "type": "apiKey",
      "in": "header",
      "name": "x-postmark-server-token"
    }
  ]
}
```